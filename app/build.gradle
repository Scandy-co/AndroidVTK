apply plugin: 'com.android.application'

def ENABLE_JAVA_ACTIVITY = true;
def ENABLE_NATIVE_ACTIVITY = !ENABLE_JAVA_ACTIVITY;

Properties properties = new Properties()
properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
// vtk.dir should point to a dir that has vtkConfig.cmake
def vtkDir = properties.getProperty("vtk.dir", null)

android {
  compileSdkVersion 22
  buildToolsVersion "25.0.3"
  defaultConfig {
    applicationId "co.scandy.nativevtk"
    minSdkVersion 21
    targetSdkVersion 22
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    manifestPlaceholders = [ useJavaActivity:ENABLE_JAVA_ACTIVITY, useNativeActivity:ENABLE_NATIVE_ACTIVITY ]
    externalNativeBuild {
      cmake {
        arguments "-DVTK_DIR=${vtkDir}",
                  "-DENABLE_JAVA_ACTIVITY=${ENABLE_JAVA_ACTIVITY ? 'ON' : 'OFF'}",
                  "-DANDROID_TOOLCHAIN=gcc",
                  "-DCMAKE_SYSTEM_NAME=Android"
        cppFlags "-std=c++11 -frtti -fexceptions -fpermissive -fPIC -w"
      }
    }
    ndk {
      moduleName "NativeVTK"
      abiFilters "armeabi-v7a"
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
//  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.android.support:appcompat-v7:22.2.0'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  testCompile 'junit:junit:4.12'
}
